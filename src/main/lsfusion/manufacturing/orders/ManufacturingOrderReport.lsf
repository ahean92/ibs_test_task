MODULE ManufacturingOrderReport;

REQUIRE ManufacturingOrderDone, ItemAttribute;

NAMESPACE Manufacturing;

attribute(ManufacturingOrder o, ItemAttribute a) = value(attribute(item(o), a));

FORM manufacturingOrderReport 'Order report'
    OBJECTS a = ItemAttribute
    
    OBJECTS o = ManufacturingOrder PIVOT
    PROPERTIES(o) READONLY number, scheduledDateTime, nameItem, imagedNameStatus, nameType, executionDateTime, 
                           nameCompany, nameMaterialsLocation, nameProductsLocation, 
                           nameUom, descriptionBom
    PROPERTIES READONLY 'Category 1' = level1(item(o)), 'Category 2' = level2(item(o)), 'Category 3' = level3(item(o)), 'Category 4' = level4(item(o)), 
                        'Canonical group' = canonicalNameCategory(item(o)) 
    PROPERTIES READONLY attribute(o, a) COLUMNS (a) HEADER name(a)
    PROPERTIES(o) READONLY MEASURE toManufacture, manufactured
    FILTERS accessGranted(materialsLocation(o)) OR accessGranted(productsLocation(o)) 
;

DESIGN manufacturingOrderReport {
    OBJECTS {
        NEW filters FIRST { 
            caption = 'Filters';
            horizontal = TRUE;
        }
    }
}

@defineDateTimeAggregationForm(manufacturingOrderReport, o, scheduled){
    EXTEND FORM manufacturingOrderReport
        PROPERTIES(o) READONLY AFTER scheduledDateTime(o) scheduledMinute, scheduledHour, scheduledDate
    ;
    @defineDateAggregationForm(manufacturingOrderReport, o, scheduledDate, scheduled){
    EXTEND FORM manufacturingOrderReport
        PROPERTIES(o) READONLY AFTER scheduledDate(o) scheduledNameDOW, scheduledWeek, scheduledNumberMonth, scheduledNameMonth, scheduledYear
    ;
};
};
@defineDateFilterForm(manufacturingOrderReport, o, scheduled){
    EXTEND FORM manufacturingOrderReport
        OBJECTS dates = INTERVAL[DATE] BEFORE o PANEL NULL
        PROPERTIES filterDates 'Date interval' = VALUE(dates)

        FILTERS NOT scheduledDate(o) < from(dates), NOT scheduledDate(o) > to(dates)
    ;
    
    DESIGN manufacturingOrderReport {
        filters {
            MOVE PROPERTY(filterDates);
        }
    }
};

NAVIGATOR {
    manufacturing {
        reporting {
            NEW manufacturingOrderReport;        
        }
    }
}