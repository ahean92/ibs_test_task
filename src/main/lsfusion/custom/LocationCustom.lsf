MODULE LocationCustom;

REQUIRE Security, MasterData;
//        Location;

// делаем справочник подразделений с признаком цех
CLASS Location 'Подразделение';

name 'Имя' = DATA ISTRING[100] (Location) CHARWIDTH 15;

FORM location 'Подразделение'
    OBJECTS l = Location PANEL
    PROPERTIES(l) name

    EDIT Location OBJECT l
;

FORM locations 'Подразделения'
    OBJECTS l = Location
    PROPERTIES(l) READONLY name
    PROPERTIES(l) NEWSESSION NEW, EDIT, DELETE

    LIST Location OBJECT l
;

NAVIGATOR {
    masterData {
        NEW locations;
    }
}

// добавляем признак цех
workshop 'Цех' = DATA BOOLEAN (Location);

EXTEND FORM location
    PROPERTIES(l) workshop
;

//DESIGN location {
//    params {
//        MOVE PROPERTY(workshop(l));
//    }
//}

// подразделение кладовщика
in (CustomUser u, Location l) = DATA BOOLEAN; // означает, что пользователю доступно подразделение
locations 'Подразделения' (CustomUser u) = GROUP CONCAT name(Location l) IF in(u, l), ', ' ORDER l;

firstStock (CustomUser u) = GROUP MIN Location l IF in(u, l) AND NOT workshop(l);

adminRole() = userRoleSID('admin');

access(CustomUser u, Location l) = in(u, l) OR has(u, adminRole());
access(Location l) = access(currentUser(), l);

// добавляем список подразделений на форму с пользователями для возможности редактирования
EXTEND FORM customUser PROPERTIES(u) locations;
DESIGN customUser {
    info {
        MOVE PROPERTY(locations(u));
    }
}
