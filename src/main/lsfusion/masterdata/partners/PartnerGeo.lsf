MODULE PartnerGeo;

REQUIRE Partner, Geo;

NAMESPACE MasterData;

META defineObjectMap(object, lobj, container)
    latitude 'Latitude'= DATA NUMERIC[11,6] (###object);
    longitude 'Longitude' = DATA NUMERIC[11,6] (###object);

    inputGoogleAddress (###object p) {
        INPUT a = STRING[150] CUSTOM 'googleAutocomplete' DO {
            address(p) <- getWord(a, ',', 1);
            city(p) <- getWord(a, ',', 2);
            state(p) <- getWord(a, ',', 3);

            getCoordinates(a);

            latitude(p) <- readLatitude();
            longitude(p) <- readLongitude();
        }
    }

    EXTEND FORM object
        PROPERTIES addressCsv(lobj) ON CHANGE inputGoogleAddress(lobj) SHOWIF apiKey(MapProvider.google)
        PROPERTIES(lobj) latitude, longitude

        OBJECTS lobj##g = ###object MAP showMapProvider
        PROPERTIES(lobj##g) name READONLY, latitude, longitude
        FILTERS lobj##g = lobj
    ;

    DESIGN object {
        container {
            NEW geo {
                caption = 'Coordinates';
                MOVE PROPERTY(addressCsv(lobj)) { alignment = STRETCH; }
                NEW coordinates {
                    horizontal = TRUE;
                    MOVE PROPERTY(latitude(lobj));
                    MOVE PROPERTY(longitude(lobj));
                }
                MOVE BOX(lobj##g) {
                    height = 500;
                    caption = '';
                }
            }
        }
    }
END

@defineObjectMap(partner, p, tabs);

EXTEND FORM partners
    PROPERTIES(p) READONLYIF isReadonly() latitude, longitude
;
